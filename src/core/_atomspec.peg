atom_specifier   = left:atom_specifier operator:'&' right:as_term
                 | left:atom_specifier operator:'|' right:as_term
                 | left:as_term
                 ;

as_term          = '(' term:as_term  ')'
                 | selector:selector_name
                 | models:model_list
                 ;

selector_name    = name:/[a-zA-Z_][a-zA-Z0-9_]*/
                 ;

model_list       = { model+:model }*
                 ;

model            = '#' hierarchy:model_hierarchy [ parts:model_parts ] [ zone:zone_selector ]
		 | parts:model_parts [ zone:zone_selector ]
                 ;

model_hierarchy  = range_list:model_range_list
                   { '.' hierarchy:model_range_list }*
                 ;

model_range_list = range:model_range { ',' range_list:model_range_list }*
                 ;

model_range      = start:model_spec '-' end:model_spec
                 | start:model_spec
                 ;

model_spec       = number:/[0-9]+/
                 | star:'*'
                 ;

model_parts      = { chain+:chain }+
                 ;

chain            = '/' parts:part_list { residue+:residue }*
		 | { residue+:residue }+
                 ;

residue          = ':' parts:part_list { atom+:atom }*
		 | { atom+:atom }+
                 ;

atom             = '@' parts:part_list
                 ;

part_list        = part:part_list ',' range:part_range_list
                 | range:part_range_list 
                 ;

part_range_list  = start:/-?[a-zA-Z0-9]+/ [ '-' end:/[-a-zA-Z0-9]+/ ]
                 ;

zone_selector    = zone_operator real_number
                 ;

zone_operator    = '@>='
                 | '@>'
                 | '@<='
                 | '@<'
                 | ':>='
                 | ':>'
                 | ':<='
                 | ':<'
                 | '#>='
                 | '#>'
                 | '#<='
                 | '#<'
		 ;

real_number      = /[0-9]*\.?[0-9]+/
                 ;
