(* vi: set expandtab shiftwidth=4 softtabstop=4: *)

(* Remember to use left recursion when needed, not right *)

atom_specifier   = left:as_term operator:'&' right:atom_specifier
                 | left:as_term operator:'|' right:atom_specifier
                 | left:as_term
                 ;

as_term          = '(' atomspec:atom_specifier ')'
                 | '~' tilde:as_term
                 | selector:selector_name
                 | models:model_list
                 ;

selector_name    = name:/[a-zA-Z_][a-zA-Z0-9_]*/
                 ;

model_list       = { model+:model }+
                 ;

model            = '##' attrs:attribute_list [ parts:model_parts ] [ zone:zone_selector ]
                 | '#' hierarchy:model_hierarchy [ parts:model_parts ] [ zone:zone_selector ]
		 | parts:model_parts [ zone:zone_selector ]
                 ;

model_hierarchy  = range_list:model_range_list { '.' hierarchy:model_range_list }*
                 ;

model_range_list = range:model_range { ',' range_list:model_range_list }*
                 ;

model_range      = start:model_spec '-' end:model_spec
                 | start:model_spec
                 ;

(* limit model numbers to 5 digits to avoid conflict with # colors *)

model_spec       = number:/[0-9]{1,5}(?![0-9A-Fa-f])/
                 | star:'*'
                 ;

model_parts      = { chain+:chain }+
                 ;

chain            = '//' attrs:attribute_list { residue+:residue }*
                 | '/' parts:part_list { residue+:residue }*
		 | { residue+:residue }+
                 ;

residue          = '::' attrs:attribute_list { atom+:atom }*
                 | ':' parts:part_list { atom+:atom }*
		 | { atom+:atom }+
                 ;

atom             = '@@' attrs:attribute_list
                 | '@' parts:part_list
                 ;

part_list        = range:part_range_list ',' part:part_list
                 | range:part_range_list 
                 ;

part_range_list  = start:/-?[a-zA-Z0-9]+/ [ '-' end:/[-a-zA-Z0-9]+/ ]
                 ;

attribute_list   = attr_test { ',' attr_test }*
                 ;

attr_test        = name:attr_name op:attr_operator value:attr_value
                 | name:attr_name
                 | no:'~' name:attr_name
                 ;

attr_name        = /[a-zA-Z_][a-zA-Z0-9_]*/
                 ;

attr_operator    = '>='
                 | '>'
                 | '<='
                 | '<'
                 | '='
                 | '!='
                 | '<>'
                 ;

attr_value       = ?/[^,#/:@ \t]+/?
                 ;

zone_selector    = zone_operator real_number
                 ;

zone_operator    = '@>='
                 | '@>'
                 | '@<='
                 | '@<'
                 | ':>='
                 | ':>'
                 | ':<='
                 | ':<'
                 | '/>='
                 | '/>'
                 | '/<='
                 | '/<'
                 | '#>='
                 | '#>'
                 | '#<='
                 | '#<'
		 ;

real_number      = /[0-9]*\.?[0-9]+/
                 ;
