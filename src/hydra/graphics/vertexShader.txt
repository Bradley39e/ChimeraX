#version 330 core

#if defined(USE_TEXTURE_MASK) || defined(USE_DEPTH_OUTLINE)

in vec3 position;
in vec2 tex_coord_2d;
out vec2 tex_coord;
void main(void)
{
  tex_coord = tex_coord_2d;
  gl_Position = vec4(position,1);
}

#else

uniform mat4 model_view_matrix;
uniform mat4 projection_matrix;

in vec3 position;

#ifdef USE_LIGHTING
in vec3 normal;
out vec3 N;
out vec3 v;
#endif

#ifdef USE_VERTEX_COLORS
in vec4 vcolor;
out vec4 color;
#endif

#ifdef USE_TEXTURE_2D
in vec2 tex_coord_2d;
out vec2 tex_coord;
#endif

#ifdef USE_INSTANCING_SS
in vec4 instanceShiftAndScale;
#endif

#ifdef USE_INSTANCING_44
in mat4 instancePlacement;
#endif

#ifdef USE_TEXTURE_3D_AMBIENT
uniform mat4 ambient_tex3d_transform;
out vec3 ambient_tex_coord;
#endif

#ifdef USE_SHADOWS
uniform mat4 shadow_transform;
out vec3 shadow_tex_coord;
#endif

void main(void)
{
#ifdef USE_INSTANCING_SS
  vec4 vi = vec4(instanceShiftAndScale.w * position + instanceShiftAndScale.xyz, 1);
  vec4 vEye = model_view_matrix * vi;
#else
  #ifdef USE_INSTANCING_44
    vec4 vi = instancePlacement * vec4(position, 1);
    vec4 vEye = model_view_matrix * vi;
  #else
    vec4 vi = vec4(position,1);
    vec4 vEye = model_view_matrix * vi;
  #endif
#endif

#ifdef USE_LIGHTING
  v = vEye.xyz;
  #ifdef USE_INSTANCING_44
    vec3 ni = vec3(instancePlacement * vec4(normal,0));
    N = normalize((model_view_matrix * vec4(ni,0)).xyz);
  #else
    N = normalize((model_view_matrix * vec4(normal,0)).xyz);
  #endif
#endif

#ifdef USE_TEXTURE_2D
  tex_coord = tex_coord_2d;
#endif

#ifdef USE_TEXTURE_3D_AMBIENT
  ambient_tex_coord = (ambient_tex3d_transform * vi).xyz;
#endif

#ifdef USE_SHADOWS
  shadow_tex_coord = (shadow_transform * vEye).stp;
#endif

#ifdef USE_VERTEX_COLORS
  color = vcolor;
#endif

  gl_Position = projection_matrix * vEye;
}

#endif	// USE_IMAGE_BLEND
